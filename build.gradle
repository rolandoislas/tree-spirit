buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "com.matthewprenger.cursegradle"

ext.build = System.getenv('TRAVIS_BUILD_NUMBER')
ext.tag = System.getenv("TRAVIS_TAG")
version = "1.12.2-1.3"
version =  version + "-build-T" + ext.build
group= "com.rolandoislas.treespirit" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "treespirit"

minecraft {
    version = "1.12.2-14.23.0.2491"
    runDir = "run"
    mappings = "snapshot_20170626"
    replace("@VERSION@", version)
}

if (ext.tag) {
    curseforge {
        apiKey = System.getenv("CURSE_API_KEY") ?: ""
        ext.tag = System.getenv("TRAVIS_TAG") ?: ""
        project {
            id = "261747"
            changelog = System.getenv("TRAVIS_COMMIT_MESSAGE") ?: ""
            if (ext.tag.contains("release"))
                releaseType = "release"
            else if (ext.tag.contains("beta"))
                releaseType = "beta"
            else
                releaseType = "alpha"
            mainArtifact jar
            relations {
                optionalLibrary "touch-of-beacon"
            }
        }
    }
}

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

idea.module.inheritOutputDirs = true

dependencies {
    compile fileTree(dir: "lib", include: ["*.jar"])
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
